allprojects {
    group 'com.wilmol'
    version = '1.0.0'
}

buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'com.diffplug.spotless:spotless-plugin-gradle:3.22.0'
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'com.diffplug.gradle.spotless'
    apply plugin: 'jacoco'

    sourceCompatibility = 11.0
    targetCompatibility = 11.0

    spotless {
        java {
            removeUnusedImports()
            importOrderFile rootProject.file('gradle/spotless/import-order')
            eclipse().configFile rootProject.file('gradle/spotless/code-formatting.xml')
        }
    }

    clean {
        // delete intellij build directories too
        delete 'out/'
    }

    test {
        maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
        useJUnitPlatform()
        testLogging {
            exceptionFormat 'full'
            events 'failed'
        }
    }

    jacocoTestReport {
        reports {
            xml.enabled true
            html.enabled true
            csv.enabled true
        }
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation group: 'com.google.guava', name: 'guava', version: '28.0-jre'
        implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.11.2'
        implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.9'
        implementation group: 'org.javatuples', name: 'javatuples', version: '1.2'
        implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.5'

        implementation group: 'org.projectlombok', name: 'lombok', version: '1.18.8'
        annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.8'
        
        testImplementation (group: 'com.google.truth', name: 'truth', version: '0.45'){
//            exclude group: 'org.hamcrest'
        }
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.5.0-RC1'
        testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.5.0-RC1'
    }
}
